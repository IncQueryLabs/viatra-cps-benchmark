/*******************************************************************************
 * Copyright (c) 2014-2016 IncQuery Labs Ltd.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     Akos Horvath, Abel Hegedus, Zoltan Ujhelyi, Peter Lunk - initial API and implementation
 *******************************************************************************/
module com.incquerylabs.examples.cps.integration.performance.eventdriven

import org.eclipse.viatra.integration.mwe2.*
import org.eclipse.viatra.integration.mwe2.mwe2impl.*
import com.incquerylabs.examples.cps.integration.performance.*
import com.incquerylabs.examples.cps.integration.performance.batch.*
import com.incquerylabs.examples.cps.integration.performance.eventdriven.*
import org.eclipse.viatra.examples.cps.integration.messages.*
import org.eclipse.viatra.examples.cps.integration.*

Workflow {
	component = PerformanceInitializerComponent{
		outputProjectLocation = "D:\\MWE_TEST"
		outputProjectName = "VIATRA_ED"
	}
	
	component = TransformationChain {
		step = ForLoop{
			iterations = "2"
			
			step = PerformanceEventDrivenViatraTransformationStep {}
			step = ChangeMonitorTransformationStep{
				publishing = PublishTo{topicName = "M2TTopic" factory = ChangeDeltaMessageFactory{}}
			}
			step = PerformanceM2TDistributedTransformationStep{
				subscription = SubscribeTo{topicName = "M2TTopic" processor = ChangeDeltaMessageProcessor{}}
				publishing = PublishTo{topicName = "SerializeTopic" factory = M2TOutputMessageFactory{}}
			}
			step = PerformanceSerializerTransformationStep{
				subscription = SubscribeTo{topicName = "SerializeTopic" processor = M2TOutputMessageProcessor{}}
			}
			step = ModelModifierStep {}
		}
	}
}
